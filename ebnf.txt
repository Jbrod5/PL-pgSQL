NOTACION EBNF:
| -> or
[] -> opcional (cero o una vez)
{} -> repeticion (cero o mÃ¡s veces)
() -> agrupacion 
"..."_"..." -> rango de caracteres (como [a-z])
; -> final de regla

- - - - - - - - - - - - - - - - - - - - COSAS GENERALES - - - - - - - - - - - - - - - - - - - -

letra = ("a"_"z"|"A"_"Z");
digito = "0"_"9";
equals = "=";
dot = ".";
all_sym = "*";

identificador = (letra | "_") {letra | digito | "_"};

numero = digito {digito};
booleano = "true" | "false";
string = '"' (~["\r\n])* '"';
valor_op = numero | booleano | string;

opciones = identificador { comma identificador };
valores = valor_op { comma valor_op };

- - - - - - - - - - - - - - - - - DDL (Data Definition Language) - - - - - - - - - - - - - - - - -

// Palabras reservadas
create = "CREATE";
schema = "SCHEMA";
use = "USE";
table = "TABLE";
alter = "ALTER";
add = "ADD";
column = "COLUMN";
tipo = "TIPO";
drop =  "DROP";
constraint = "CONSTRAINT";
foreign = "FOREIGN";
primary = "PRIMARY";
key = "KEY";
references = "REFERENCES";

integer = "INTEGER";
varchar = "VARCHAR";
boolean = "BOOLEAN";
par_opn = "(";
par_cls = ")";
pnt_cma = ";";
comma = ",";

not = "NOT";
null = "NULL";

// Estructura del DDL
create_schema = create schema identificador;
use_schema = use schema identificador;

tipo_dato = integer | varchar "(" digito {digito} ")" | boolean;
restriccion_columna = [ primary key ] [ not null | null ];
columna = identificador tipo_dato restriccion_columna;

llave_foranea = foreign key par_opn identificador par_cls references identificador par_opn identificador par_cls;

elemento_tabla = columna | llave_foranea;
lista_elementos = elemento_tabla { comma elemento_tabla };

create_table = create table identificador par_opn lista_elementos par_cls pnt_cma;

alter_add_column = alter table identificador add column columna pnt_cma;
alter_drop_column = alter table identificador drop column identificador pnt_cma;
alter_add_constraint = alter table identificador add constraint identificador llave_foranea pnt_cma;
alter_drop_constraint = alter table identificador drop constraint identificador pnt_cma;

drop_table = drop table identificador pnt_cma;

- - - - - - - - - - - - - - DML (Data Manipulation Language) - - - - - - - - - - - - - - - - -

insert  = "INSERT";
into    = "INTO";
values  = "VALUES";
update  = "UPDATE";
set     = "SET";
where   = "WHERE";
delete  = "DELETE";
from    = "FROM";
select  = "SELECT";
join    = "JOIN";
left_join = "LEFT JOIN";
on = "ON";

insert_into = insert into identificador par_opn opciones par_cls values par_opn valores par_cls;

asignaciones = identificador equals valor_op { comma identificador equals valor_op };
update_sentence = update identificador set asignaciones where identificador equals valor_op;

delete_sentence = delete from identificador where identificador equals valor_op;

columna_prefijo = [ identificador dot ] identificador;
columnas = columna_prefijo { comma columna_prefijo };

join_type = join | left_join;
join_clause = join_type identificador on columna_prefijo equals columna_prefijo;
join_list = join_clause { join_clause };

select_sentence = select columnas from identificador { join_list } [ where columna_prefijo equals valor_op ];

- - - - - - - - - - - - - - DCL (Data Control Language) - - - - - - - - - - - - - - - - -

permiso = select | insert | update | delete;

user = "USER";
grant = "GRANT";
on = "ON";
to = "TO";
revoke = "REVOKE";

tabla_op = identificador | all_sym;  

create_user = create user identificador pnt_cma;
grant_perm = grant permiso on identificador dot tabla_op to identificador pnt_cma;
revoke_perm = revoke permiso on identificador from identificador pnt_cma;
